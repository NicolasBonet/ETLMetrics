
pre {
	var resource = new android!Resource;
	var urlBase = "http://157.253.191.41/";
	var programBase = "";
}

rule Application2Application
	transform sa : snap!Application
	to aa : android!Application {
		var appName = new android!String;
		appName.name = "app_name";
		appName.value = sa.name; 
		resource.strings.add(appName);
		programBase = sa.name.toLowerCase().replace(" ","-");
		var program = new android!String;
		program.name = "program";
		program.value = programBase; 
		resource.strings.add(program);
		aa.resources = resource;
		aa.activities = sa.programs.get(0).entities.equivalent();
	}

@lazy
rule Entity2Activity
	transform e : snap!Entity
	to a : android!Activity {
		var title = new android!String;
		title.name = toStringId(e.name);
		title.value = e.name.toLowerCase().firstToUpperCase(); 
		resource.strings.add(title);
		a.title = title;
		var url = new android!String;
		url.name = "url_" + e.name.toLowerCase();
		url.value = urlBase + programBase + "/views/" + e.name.toLowerCase() + ".html"; 
		resource.strings.add(url);
		var layout = getLayout(toStringId(e.name));
		a.content = layout;
	}

operation getLayout(id : String) : android!Layout {
	var layout = new android!Layout;
	layout.id = id;
	return layout;
}

operation toStringId(text : String) : String {
	var result = new String;
	result = text.replace(" ", "_");
	result = result.toLowerCase();
	return result;
}