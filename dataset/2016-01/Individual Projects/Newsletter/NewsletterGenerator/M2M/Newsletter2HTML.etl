model newsletter driver EMF {path='Newsletter.ecore'};
model html driver EMF {path='HTML.ecore'};

pre {
	"Start ETL Newsletter2HTML".println();
}
rule Newsletter2HTML
	transform
		s:newsletter!Newsletter
	to
		t:html!HTML{
			if(s.table <> null){
				var table = s.table.equivalent();
				t.htmlElements.add(table);
			}
		}

@lazy
rule Table2TABLE
	transform
		s:newsletter!Table
	to
		t:html!TABLE{
			if(s.width <> null and s.width > 0){
				t.width = s.width.asString();
			}
			if(s.backgroundColor <> null){
				t.bgcolor = s.backgroundColor.literal;
			}
			t.align = "center";
			t.border = 0;
			t.cellpadding = "0";
			t.cellspacing = "0";
			for(row in s.rows){
				t.addElement(row.equivalent());
			}
		}

@lazy
rule Row2TR
	transform
		s:newsletter!Row
	to
		t:html!TR{
			if(s.height <> null and s.height > 0){
				t.height = s.height.asString();
			}
			if(s.backgroundColor <> null){
				t.bgcolor = s.backgroundColor.literal;
			}
			if(s.cells.size() > 1){
				var tr = new html!TR;
				for(cell in s.cells){
					tr.addElement(cell.equivalent());
				}
				t.addElementRecursively(new html!TD);
				var table = new html!TABLE;
				table.cellspacing = "0";
				table.cellpadding = "0";
				t.addElementRecursively(table);
				t.addElementRecursively(tr);
			} else if(s.cells.size() == 1){
				t.addElement(s.cells.get(0).equivalent());			
			} else if(s.cells.isEmpty()){
				t.addElement(new html!TD);
			}
		}

@lazy		
rule Cell2TD
	transform
		s:newsletter!Cell
	to
		t:html!TD{
			if(s.width <> null and s.width > 0){
				t.width = s.width.asString();
			}
			if(s.backgroundColor <> null){
				t.bgcolor = s.backgroundColor.literal;
			}
			t.valign = s.verticalAlignment.literal;
			for(element in s.elements){
				var htmlElement : html!HTMLElement;
				if(element.isTypeOf(newsletter!Text)){
					htmlElement = element.getHtmlTextElement();
				} else if(element.isTypeOf(newsletter!Button)){
					htmlElement = element.getHtmlButtonElement();
				} else if(element.isTypeOf(newsletter!Image)){
					htmlElement = element.getHtmlImageElement();
				} else if(element.isTypeOf(newsletter!Video)){
					htmlElement = element.getHtmlVideoElement();
				} else if(element.isTypeOf(newsletter!Divider)){
					htmlElement = element.getHtmlDividerElement();
				} 
				t.addElement(htmlElement);
			}
		}

post {
	"Finish ETL Newsletter2HTML".println();
}
		
operation newsletter!Text getHtmlTextElement() : html!HTMLElement{
	var p = new html!P;
	p.align = self.align.literal;
	if(self.height <> null and self.height > 0){
		p.addStyle(html!StyleKey#lineHeight, self.height.toString() + "px");
	}
	if(self.height <> null and self.width > 0){
		p.addStyle(html!StyleKey#width, self.width.toString() + "px");
	}
	if(self.textAlign <> null){
		p.addStyle(html!StyleKey#textAlign, self.textAlign.literal);
	}
	
	//Add A element if applies
	if(self.link <> null){
		var a = new html!A;
		a.ref = self.link;
		a.addStyle(html!StyleKey#textDecoration, "none");
		p.addElementRecursively(a);
	}
	
	//Add B element if applies
	if(self.bold){
		p.addElementRecursively(new html!B);
	}
	
	//Add I element if applies
	if(self.italic){
		p.addElementRecursively(new html!I);
	}
	
	//Add U element if applies
	if(self.underline){
		p.addElementRecursively(new html!U);
	}
	
	//Add S element if applies
	if(self.strikethrough){
		p.addElementRecursively(new html!S);
	}
	
	var font = new html!FONT;
	font.value = self.content;
	font.face = self.fontFamily.literal;
	font.size = self.fontSize;
	font.color = self.fontColor.literal;
	font.addStyle(html!StyleKey#padding, "5px");
	p.addElementRecursively(font);
	return p;
}

operation newsletter!Button getHtmlButtonElement() : html!HTMLElement{
	if(self.link <> null){
		self.text.link = self.link;
	}
	var p = self.text.getHtmlTextElement();
	p.addStyle(html!StyleKey#textAlign, self.align.literal);
	
	if(p.elements.get(0).isTypeOf(html!A)){
		//Button with link
		var a = p.elements.get(0);
		a.addButtonStyles(self);
	} else {
		//Button without link
		p.addButtonStyles(self);
	}
	
	return p;
}

operation html!HTMLElement addButtonStyles(button : newsletter!Button) {
	//Dynamic Styles
	self.addStyle(html!StyleKey#backgroundColor, button.color.literal);
	self.addStyle(html!StyleKey#lineHeight, button.height.toString() + "px");
	self.addStyle(html!StyleKey#width, button.width.toString() + "px");
	//Static Styles
	self.addStyle(html!StyleKey#display, "inline-block");
	self.addStyle(html!StyleKey#textDecoration, "none");
	self.addStyle(html!StyleKey#textAlign, "center");
} 

operation newsletter!Image getHtmlImageElement() : html!HTMLElement{
	var p = new html!P;
	p.align = self.align.literal;
	var img = new html!IMG;
	img.src = self.src;
	
	//Set image width
	if(self.width <> null and self.width > 0){
		img.width = self.width.toString();
	} else {
		img.width = "100%";
	}
	
	//Set image height
	if(self.height <> null and self.height > 0){
		img.height = self.height.toString();
	} else {
		img.height = "100%";
	}
	
	img.border = "0";
	
	//Set image link
	var a = new html!A;
	if(self.link <> null){
		a.ref = self.link;
	} else {
		a.ref = self.src;
	}
	p.addElementRecursively(a);
	
	p.addElementRecursively(img);
	return p;
}

operation newsletter!Video getHtmlVideoElement() : html!HTMLElement{
	var preview = self.preview.getHtmlImageElement();
	var title = self.title.getHtmlTextElement();
	var button = self.button.getHtmlButtonElement();
	
	if(self.link <> null){
		self.preview.link = self.link;
		self.button.link = self.link;
	}
	
	var div = new html!DIV;
	div.align = self.align.literal;
	div.addElement(preview);
	div.addElement(title);
	div.addElement(button);
	div.addStyle(html!StyleKey#padding, "20px");
	return div;
}

operation newsletter!Divider getHtmlDividerElement() : html!HTMLElement{
	var table = new html!TABLE;
	table.cellpadding = "10";
	table.cellspacing = "0";
	table.width = "100%";
	
	var hr = new html!HR;
	hr.color = self.color.literal; 
	
	table.addElement(new html!TR);
	table.addElementRecursively(new html!TD);
	table.addElementRecursively(hr);
	
	return table;
}

operation html!HTMLElement addElement(element : html!HTMLElement){
	self.elements.add(element);
}

operation html!HTMLElement addElementRecursively(element : html!HTMLElement){
	if(self.elements.isEmpty()){
		self.addElement(element);
	} else {
		self.elements.get(0).addElementRecursively(element);
	}
}

operation html!HTMLElement addStyle(key : Any, value : String){
	var style = new html!Style;
	style.key = key;
	style.value = value;
	
	var found = false;
	for(style in self.styles){
		if(style.key == key){
			found = true;
			style.value = value;
		}
	}
	
	if(not found){
		self.styles.add(style);
	}
	
}

operation html!HTMLElement removeStyle(key : Any){
	for(style in self.styles){
		if(style.key == key){
			self.styles.remove(style);
		}
	}
}