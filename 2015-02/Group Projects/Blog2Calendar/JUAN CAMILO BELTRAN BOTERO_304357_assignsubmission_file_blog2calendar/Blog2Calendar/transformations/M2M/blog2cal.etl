rule blog2cal
	transform
		f : bl!Blog
	to
		t : cl!Calendar
		{
			var events = f.categories.collect(c|c.articles).flatten().equivalent();
			t.years = events.collect(e|e.day.month.year);
	    }
	   
rule article2event
	transform
		f : bl!Article
	to
		e : cl!Event
		{
			e.event = f.summary;
			e.owner = f.author.collect(a|a.name).concat(" ");
			e.tags = f.tags.collect(t|t.tag).concat(" ");
			
			var day = f.getDay();
			e.day = day;
			day.events.add(e);
	    }
	    
operation bl!Article getDay()
{
	var dateSeq = self.date.split("/");
	var years = cl!Year.all.select(y|y.year = dateSeq[2]);
	var year;
	
	if (years.size() > 0) {
		year = years[0];
	}
	else{
		year = new cl!Year();
		year.year = dateSeq[2].asInteger();
	}
	
	var months = year.months.select(m|m.month = dateSeq[1]);
	var month;

	if (months.size() > 0) {
		month = months[0];
	}
	else{
		month = new cl!Month();
		month.month = dateSeq[1].asInteger();
		month.year = year;
		year.months.add(month);
	}

	var days = month.days.select(d|d.day = dateSeq[0]);
	var day;

	if (days.size() > 0) {
		day = days[0];
	}
	else{
		day = new cl!Day();
		day.day = dateSeq[0].asInteger();
		day.month = month;
		month.days.add(day);
	}

	return day;
}