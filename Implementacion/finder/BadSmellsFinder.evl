/*
* Settings
*/
pre{
	var threshold_complex_ocl = 5;
	var threhold_output_code = 5;
}

/*
* Rules
*/
context ETL!TransformationRule
{
	/*
		Bad Smell: Name convention
	*/
	critique NameShouldStartWithUpperCase
	{
		guard : self.name.name <> ""
		
		check : self.name.name.substring(0,1) = self.name.name.substring(0,1).toUpperCase()
	    
	    message : "[02] The name of transformation rule " + self.name.name + " should start with a upper-case letter"
    }
    
    critique SourceNameShouldStartWitLowerCase
	{
		guard : self.source.name.name <> ""
	
		check : self.source.name.name.substring(0,1) = self.source.name.name.substring(0,1).toLowerCase()
	    
	    message : "[01] The source parameter " + source.name.name + " of transformation rule " + self.name.name + " should start with a lower-case letter"
    }
    
    critique TargetNameShouldStartWitLowerCase
	{
		guard : self.targets.size() > 0
	
		check : self.targets.select(target | target.name.name <> "" and target.name.name.substring(0,1) <> target.name.name.substring(0,1).toLowerCase()).size() = 0
	    
	    message : "[01] All target parameters of transformation rule " + self.name.name + " should start with a lower-case letter"
    }
    
    /*
	* Bad Smell: Avoid output code
	*/
	critique AvoidOutputCode
	{
		check {
			var nameOcurrences = new Map;
			var nameVariable = "";
			for (nameStatement in self.body.statements.select(s | s.hasProperty('lhs') and s.hasProperty('rhs') and ((s.lhs.hasProperty('target') and s.lhs.target.isTypeOf(ETL!NameExpression)) or s.lhs.isTypeOf(ETL!NameExpression)) and s.rhs.isTypeOf(ETL!PlusOperatorExpression) and s.rhs.hasProperty('rhs') and s.rhs.rhs.isTypeOf(ETL!StringExpression)))
			{
				// Get the name
				if (nameStatement.lhs.hasProperty('target'))
					nameVariable = nameStatement.lhs.target.name;
				else 
					nameVariable = nameStatement.lhs.name;
				
				if (nameOcurrences.containsKey(nameVariable) and nameOcurrences.get(nameVariable) >= threhold_output_code)
					return false;
				else if (nameOcurrences.containsKey(nameVariable))
					nameOcurrences.put(nameVariable, nameOcurrences.get(nameVariable) + 1);
				else
					nameOcurrences.put(nameVariable, 1);
			}
			
			return true;
		}
		
		message : "[11] On rule " + self.name.name + " the variable " + nameVariable + " is concatenated multiple times, make sure you're not adding output code on this transformation."
	}
	
	/*
		Bad Smell: Split Rules
	*/
	critique SplitRules
	{
		guard : self.body.statements.size() = 1
		
		check : self.body.statements.select(s | s.isTypeOf(ETL!IfStatement)).size() = 0
		
		message : "[07] The rule " + self.name.name + " (between lines " + self.region.start.line + " and " + self.region.end.line + ") has an if as its only child, consider the use of guards to factorize it."
    }
    
    /*
		Bad Smell: Merge Rules
	*/
	critique MergeRules
	{
		guard : self.container.transformationRules.size() > 1
		
		check {
		
			// Variables to enhance warning
			var ruleCompared = "";
			var ruleComparedStart = 0;
			var ruleComparedEnd = 0;
		 
		 	// Get rule bindings
		 	var ruleStatements = self.body.statements.select(s | s.isTypeOf(ETL!AssignmentStatement) and s.lhs.isTypeOf(ETL!PropertyCallExpression) and s.rhs.isTypeOf(PropertyCallExpression));
			var ruleSequence : new Sequence;
			
			for (rs in ruleStatements)
			{
				ruleSequence.add(rs.lhs.target.name + "." + rs.lhs.property.name + "=" + rs.rhs.target.name + "." + rs.rhs.property.name);
			}
		 	
			// Now verify if they are similar to other rules
			for (comparisonRule in self.container.transformationRules.select(s | s <> self))
			{
				// Get all bindings
				var comparisonRuleStatements = comparisonRule.body.statements.select(s | s.isTypeOf(AssignmentStatement) and s.lhs.isTypeOf(ETL!PropertyCallExpression) and s.rhs.isTypeOf(PropertyCallExpression));
				var comparisonRuleSequence : new Sequence;
				var clonedRuleSequence = ruleSequence;
				ruleCompared = comparisonRule.name.name;
				ruleComparedStart = comparisonRule.region.start.line;
				ruleComparedEnd = comparisonRule.region.end.line;
				
				// Now see if they have them!
				for (rs in comparisonRuleStatements)
				{
					clonedRuleSequence.remove(rs.lhs.target.name + "." + rs.lhs.property.name + "=" + rs.rhs.target.name + "." + rs.rhs.property.name);
				}
				
				// All included?
				if (clonedRuleSequence.isEmpty())
					return false;
			}
			
			return true;
		}
	    
	    message : "[05] Transformation rule " + self.name.name + " (between lines #" + self.region.start.line + " and #" + self.region.end.line + ") can be merged with rule " + ruleCompared + "(between lines #" + ruleComparedStart + " and #" + ruleComparedEnd + ")"
    }
}

/*
* Operations
*/
context ETL!OperationDefinition
{
	/*
		Bad Smell: Name convention
	*/
	critique NameShouldStartWithLowerCase
	{
		guard : self.name.name <> ""
		
		check : self.name.name.substring(0,1) = self.name.name.substring(0,1).toLowerCase()
	    
	    message : "[02] The name of operation " + self.name.name + " (line #" + self.region.start.line  + ") should start with a lower-case letter"
    }
    
    /*
		Bad Smell: Inline Operations
	*/
	critique UseInlineOperations
	{
		guard : self.name.name <> ""
		
		check : ETL!MethodCallExpression.allInstances.select(c | c.hasProperty('method') and c.method.name = self.name.name).size() <> 1
	    
	    message : "[04] Operation " + self.name.name + " (line #" + self.region.start.line  + ") is only being used once, consider to remove it and place content inside the parent: " + ETL!MethodCallExpression.allInstances.select(c | c.hasProperty('method') and c.method.name = self.name.name).size()
    }
    
    /*
	* Bad Smell: Avoid output code
	*/
	critique AvoidOutputCode
	{
		check {
			var nameOcurrences = new Map;
			var nameVariable = "";
			for (nameStatement in self.body.statements.select(s | s.hasProperty('lhs') and s.hasProperty('rhs') and ((s.lhs.hasProperty('target') and s.lhs.target.isTypeOf(ETL!NameExpression)) or s.lhs.isTypeOf(ETL!NameExpression)) and s.rhs.isTypeOf(ETL!PlusOperatorExpression) and s.rhs.hasProperty('rhs') and s.rhs.rhs.isTypeOf(ETL!StringExpression)))
			{
				// Get the name
				if (nameStatement.lhs.hasProperty('target'))
					nameVariable = nameStatement.lhs.target.name;
				else 
					nameVariable = nameStatement.lhs.name;
				
				if (nameOcurrences.containsKey(nameVariable) and nameOcurrences.get(nameVariable) >= threhold_output_code)
					return false;
				else if (nameOcurrences.containsKey(nameVariable))
					nameOcurrences.put(nameVariable, nameOcurrences.get(nameVariable) + 1);
				else
					nameOcurrences.put(nameVariable, 1);
			}
			
			return true;
		}
		
		message : "[11] On operation " + self.name.name + " the variable " + nameVariable + " is concatenated multiple times, make sure you're not adding output code on this transformation."
	}
}

/*
* For statements
*/
context ETL!ForStatement
{
	/*
		Bad Smell: Elements creation in rules
	*/
	critique ElementsCreationInLoops
	{
		guard : self.body.block.statements.select(s | s.isTypeOf(ETL!AssignmentStatement)).size() > 0
		
		check : self.body.block.statements.select(s | s.isTypeOf(ETL!AssignmentStatement) and (s.lhs.isTypeOf(ETL!NewExpression) or s.rhs.isTypeOf(ETL!NewExpression))).size() = 0
		
		message : "[13] The for statement (between lines " + self.region.start.line + " and " + self.region.end.line + ") is creating elements, consider to create these as rules."
    }
    
    /*
		Bad Smell: Avoid for/if in favor of OCL Expressions
	*/
	critique AvoidForIfInFavorOfOCLExpressions
	{
		guard : self.body.block.statements.size() = 1
		
		check : self.body.block.statements.select(s | s.isTypeOf(ETL!IfStatement)).size() = 0
		
		message : "[12] The for statement (between lines " + self.region.start.line + " and " + self.region.end.line + ") has an if as its only child, consider the use of OCL Expressions to factorize it."
    }
}

/*
* If statements
*/
context ETL!IfStatement
{
	/*
		Bad Smell: Substituting IF/ELSE Chains with Switch
	*/
	critique SubstitutingIFELSEChainsWithSwitch
	{
		check : self.eContents.select(c | c.isTypeOf(ETL!ExpressionOrStatementBlock) and c.block.statements.exists(s | s.isTypeOf(ETL!AssignmentStatement))).collect( c | c.block.statements.select(s | s.isTypeOf(ETL!AssignmentStatement) and s.lhs.hasProperty('property')).lhs.property.name )->asSet().size() <> 1
		
		message : "[16] The if statement (between lines " + self.region.start.line + " and " + self.region.end.line + ") can be replaced with a Switch or a Hashmap."
    }
}

/*
* Block
*/
context ETL!Block
{
	/*
		Bad Smell: Merge Bindings
	*/
	critique MergeBindings
	{
		guard : self.statements.size() > 0
		
		check {
			var nameOcurrences = new Map;
			var nameVariable = "";
			var statement = new ETL!EOLElement;
			
			for (nameStatement in self.statements.select(s | s.hasProperty('lhs') and s.lhs.isTypeOf(ETL!PropertyCallExpression)) )
			{
				// Get the name
				statement = nameStatement;
				nameVariable = nameStatement.lhs.property.name;
				
				// Use the map to see if it was used before
				if (nameOcurrences.containsKey(nameVariable))
					return false;
				else
					nameOcurrences.put(nameVariable, 1);
			}
		
			return true;
		}
		
		message : "[08] The target feature " + nameVariable + " being set at lines " + statement.region.start.line + " and " + statement.region.end.line + " was already used before, consider to merge those bindings."
    }
}

/*
* Method Call Expressions (OCL for example)
*/
context ETL!MethodCallExpression
{
	/*
		Bad Smell: Extract complex OCL in Operations
	*/
	critique ExtractComplexOCLInOperations
	{
		guard : self.eContents.size() > 0
		
		check : self.countComplexOperations(0) < threshold_complex_ocl
		
		message : "[03] The OCL expression (between lines " + self.region.start.line + " and " + self.region.end.line + ") has too many OCL expressions, try to split it into operations."
    }
    
    /*
		Bad Smell: Replace allInstances with navigation
	*/
	critique ReplaceAllInstancesWithNavigation
	{
		check : self.method.name <> "allInstances"
		
		message : "The transformation is using allInstances (between lines " + self.region.start.line + " and " + self.region.end.line + "), consider to use OCL navigation instead."
    }
    
    /*
		Bad Smell: Replace select/first with any
	*/
	critique ReplaceSelectFirst
	{
		guard : self.hasProperty('target') and self.target.hasProperty('method') and self.target.method.hasProperty('name') 
	
		check : self.method.name <> "first" or self.target.method.name <> "select"
		
		message : "[19] The method between lines " + self.region.start.line + " and " + self.region.end.line + " is using a select()->first() navigation, use selectFirst() instead for better performance."
    }
}

/*
* Property Call Expression (calls to properties, useful to know model navigation)
*/
context ETL!PropertyCallExpression
{
	/*
		Bad Smell: Protect unsafe target navigation
	*/
	critique ProtectUnsafeTargetNavigation
	{
		guard : self.container.hasProperty('condition') and self.container.condition == self
	
		check {
			var parentRule = self.findParentRule();
			
			if (parentRule == null)
				return true;
				
			return parentRule.targets.exists( t | t.name.name = self.target.name ) == false;
		}
		
		message : "[15] The rule " + parentRule.name.name + " (between lines " + parentRule.region.start.line + " and " + parentRule.region.end.line + ") has target navigation (attribute: " + self.target.name + ") which is unsafe as those variables could be without definition, consider the use of lazy rules and equivalents to control the flow of the execution."
    }
    
    /*
		Bad Smell: Replace allInstances with navigation
	*/
	critique ReplaceAllInstancesWithNavigation
	{
		check : self.property.name <> "allInstances"
		
		message : "[20] The transformation is using allInstances (between lines " + self.region.start.line + " and " + self.region.end.line + "), consider to use OCL navigation instead."
    }
}

/*
* Operations
*/
operation Any findParentRule(): ETL!TransformationRule
{
	if (self.container = null or self.container.isTypeOf(ETL!ETLModule))
		return null;
	else if (self.container.isTypeOf(ETL!TransformationRule))
		return self.container;
	else
		return self.container.findParentRule();
}

operation Any countComplexOperations(i) : Integer
{
	for (c in self.eContents)
	{
		if (c.isTypeOf(ETL!MethodCallExpression) or c.isTypeOf(ETL!FOLMethodCallExpression))
			i = i + 1;
		
		i = i + c.countComplexOperations(0);
	}
	
	return i;
}